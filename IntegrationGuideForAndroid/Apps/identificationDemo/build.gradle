// Copyright 2023 Omnissa, LLC.
// SPDX-License-Identifier: BSD-2-Clause

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 35
    namespace = "com.ws1.workspaceone_example_identification"

    defaultConfig {
        applicationId "com.omnissa.integrationguide.identification"
        minSdk 24
        targetSdk 35
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile(
                'proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        animationsDisabled = true
    }
}

// The signing configuration, which is secret and must be created by each user
// of the repository. For a template, see the readme file in this directory:  
// `workspace-one-sdk-integration-samples/IntegrationGuideForAndroid/Secret`
apply from: secretFile("keystore.gradle")

dependencies {
    // AndroidX native.
    def appcompat_version = "1.3.1"
    def constraintLayout_version = "2.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Following is added by default but is commented out for now.
    // implementation 'androidx.core:core-ktx:1.2.0'

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    // For loading and tinting drawables on older versions of the platform
    implementation "androidx.appcompat:appcompat-resources:$appcompat_version"

    implementation(
        "androidx.constraintlayout:constraintlayout:$constraintLayout_version")

    // Libraries for running automated tests.
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}

// Workspace ONE integration additions start here.

android {
    defaultConfig {
        multiDexEnabled true
    }

    kotlin {
        jvmToolchain 17
    }

    packagingOptions {
        exclude 'META-INF/kotlinx-serialization-runtime.kotlin_module'
    }
}

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/euc-releases/Android-WorkspaceONE-SDK/")
        credentials {
            /**In gradle.properties file of root project folder, add github.user=GITHUB_USERNAME  & github.token =GITHUB_ACCESS_TOKEN**/
            username = project.findProperty("github.user") ?: System.getenv("USERNAME")
            password = project.findProperty("github.token") ?: System.getenv("TOKEN")
        }
    }
    maven {
        url = uri("https://maven.pkg.github.com/euc-releases/ws1-intelligencesdk-sdk-android/")
        credentials {
            /**In gradle.properties file of root project folder, add github.user=GITHUB_USERNAME  & github.token =GITHUB_ACCESS_TOKEN**/
            username = project.findProperty("github.user") ?: System.getenv("USERNAME")
            password = project.findProperty("github.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    // Integrate Workspace ONE at the Client level.
    //
    // Before downloading, installing, or using the Omnissa Workspace ONE SDK you
    // must:
    //
    // -   Review the Omnissa Workspace ONE Software Development Kit License
    //     Agreement that is posted here.
    //     https://static.omnissa.com/sites/default/files/omnissa-sdk-agreement.pdf
    //
    //     By downloading, installing, or using the Omnissa Workspace ONE SDK you
    //     agree to these license terms. If you disagree with any of the terms,
    //     then do not use the software.
    //
    // -   Review the Omnissa Privacy Notice and the Workspace ONE UEM Privacy
    //     Disclosure for information on applicable privacy policies.
    //     https://www.omnissa.com/trust-center/
    implementation ("com.airwatch.android:airwatchsdk:${airwatchVersion}")
}

// Test task in a separate file.
apply from: 'test-no-app-uninstall.gradle'
