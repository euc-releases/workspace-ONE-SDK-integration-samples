// Copyright 2023 VMware, Inc.
// SPDX-License-Identifier: BSD-2-Clause

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.example.integrationguide"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue("string", "MODULE_NAME", buildscript.sourceFile.parentFile.name)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile(
                'proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        animationsDisabled = true
    }
}

// The signing configuration, which is secret and must be created by each user
// of the repository. For a template, see the readme file in this directory:  
// `workspace-one-sdk-integration-samples/IntegrationGuideForAndroid/Secret`
apply from: secretFile("keystore.gradle")

dependencies {
    // AndroidX native.
    def appcompat_version = "1.3.1"
    def constraintLayout_version = "2.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // Following is added by default but is commented out for now.
    // implementation 'androidx.core:core-ktx:1.2.0'

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    // For loading and tinting drawables on older versions of the platform
    implementation "androidx.appcompat:appcompat-resources:$appcompat_version"

    implementation(
        "androidx.constraintlayout:constraintlayout:$constraintLayout_version")

    // Libraries for running automated tests.
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}

task testNoAppUninstall {
    group("verification")
    description(
        "Same as testDebugUnitTest but without uninstalling the app after.")
    dependsOn(
        project.path + ':installDebug',
        project.path + ':installDebugAndroidTest'
    )
    def testPackage = android.defaultConfig.applicationId + ".test"
    def testRunner = android.defaultConfig.testInstrumentationRunner
    doFirst {
        // TOTH the exec object: https://stackoverflow.com/a/38273215/7657675
        exec {
            executable 'adb'
            args(
                'shell', 'am', 'instrument', '-e', 'disableAnalytics', 'true',
                '-w', testPackage + '/' + testRunner
            )
        }
    }
    doLast {
        exec {
            executable 'adb'
            args 'uninstall', testPackage
        }
        println("${project.path} Uninstalled test package: $testPackage")
    }
    // The following could also be used to uninstall the test package, and
    // prints a nicer message.
    //
    //     finalizedBy project.path + ':uninstallDebugAndroidTest'
    //
    // However, there seemed to be a timing issue that the uninstall would
    // occasionally still be in progress when the next installDebugAndroidTest
    // dependency was being evaluated. The outcome was that the test package
    // wasn't installed and the instrumented tests then couldn't run.

    // For the disableAnalytics business, see:
    // https://developer.android.com/training/testing/espresso/setup#analytics

    // If you get error messages like these:
    //
    //     INSTRUMENTATION_STATUS: Error=Unable to find instrumentation info for: ComponentInfo ...
    //
    //     android.util.AndroidException: INSTRUMENTATION_FAILED: ...
    //
    // Run this:
    //
    //     ./gradlew :frameworkExtendKotlin:installDebugAndroidTest
    //     adb shell pm list instrumentation
    //
    // The output of the adb should include a line like the fist one below. Copy
    // the indicated segment and paste it after the -w above.
    //
    //     instrumentation:com.example.integrationguide.test/androidx.test.runner.AndroidJUnitRunner (target=com.example.integrationguide)
    //                     <-- Copy all this ------------------------------------------------------>
    //
    // TOTH: https://stackoverflow.com/a/23394559/7657675
}
